#!/bin/awk -f
# (c) 2016-02-08 Jens Hauke <jens@4k2.de>

{
    line[lineno++] = $0;
}

END {
    printf "// Generated by \"banner_txt2c.awk %s\"\n\n", FILENAME
    max = 0;
    for (i = 0; i < 8; i++) {
	if (length(line[i]) > max) {
	    max = length(line[i]);
	}
	if (line[i]) {
	    print "// " line[i]
	}
    }
    f = FILENAME;
    gsub(/\..*/, "", f)
    printf "\n/* uint8_t data_%s[%d] = */ {\n", f, max

    for (pos = 1; pos <= max; pos++) {
	val = 0;
	valb = "";
	for (i = 0; i < 8; i++) {
	    switch (substr(line[i], pos, 1)) {
	    case "\t":
		print "ERROR: \\t in input file not allowed!"
		break;
	    case " ":
	    case "0":
	    case ".":
	    case "":
		valb = "0" valb;
		break;
	    default:
		val = or(val, lshift(1, i));
		valb = "1" valb;
	    }
	}
	printf ("\t0b%s%s // 0x%02x\n", valb, pos != max ? "," : " ", val);
    }
    printf "};\n"
}


# Local Variables:
#  compile-command: "./banner_txt2c.awk"
# End:
